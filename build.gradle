import com.github.jengelman.gradle.plugins.shadow.transformers.*

plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'org.springframework.boot' version '2.5.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'application'
}

// Can't use Boot's nested jar packaging due to environment constrained classloader
apply plugin: 'com.github.johnrengelman.shadow'

shadowJar {
    // Required for Spring
    mergeServiceFiles()
    append 'META-INF/spring.handlers'
    append 'META-INF/spring.schemas'
    append 'META-INF/spring.tooling'
    transform(PropertiesFileTransformer) {
        paths = ['META-INF/spring.factories' ]
        mergeStrategy = "append"
    }
}

def versionName

try {
    def result = new groovy.json.JsonSlurper().parseText(file('release.json').text)
    versionName = result.bot.version
} catch (ignored) {
    versionName = 'v0.0.0'
}

sourceCompatibility = '15'
group 'com.beanbeanjuice'
mainClassName = 'com.beanbeanjuice.CafeBot'
version versionName

repositories {

    // Central Maven repository.
    mavenCentral()

    maven {
        url 'https://m2.dv8tion.net/releases'
    }

}

dependencies {

    // JUnit, for Unit Testing
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.8.0-M1'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'

    // Json Slurper
    implementation group: 'org.codehaus.groovy', name: 'groovy-json', version: '3.0.8'

    // Jetbrains Annotations
    implementation group: 'org.jetbrains', name: 'annotations', version: '21.0.1'

    // Discord JDA, for Discord things
    implementation group: 'net.dv8tion', name: 'JDA', version: '4.3.0_277'

    // Twitch Connections
    implementation group: 'com.github.twitch4j', name: 'twitch4j', version: '1.5.0'

    // Lava Player
    implementation group: 'com.sedmelluq', name: 'lavaplayer', version: '1.3.77'

    // Spotify API
    implementation group: 'se.michaelthelin.spotify', name: 'spotify-web-api-java', version: '6.5.4'

    // JSOUP for Parsing
    implementation group: 'org.jsoup', name: 'jsoup', version: '1.13.1'

    // JSON
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.12.3'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.12.3'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.12.3'

    // Logback
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    // SQL Connection
    implementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.7.3'

    // Time Stuff (For Birthdays)
    implementation group: 'joda-time', name: 'joda-time', version: '2.10.10'

    // Apple Music/Itunes API
    implementation group: 'be.ceau', name: 'itunes-api', version: '4.6.0'

    // Spring Stuff
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-websocket'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-reactor-netty'
    implementation group: 'org.springframework.boot', name: 'spring-boot-maven-plugin', version: '2.5.0'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-tomcat', version: '2.5.0'
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    implementation group: 'javax.servlet', name: 'javax.servlet-api'

    // Lombok
    implementation group: 'org.projectlombok', name: 'lombok', version: '1.18.20'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.20'

}

test {
    useJUnitPlatform()
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'